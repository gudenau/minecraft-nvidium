plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
}

def gitCommitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
def buildtime = System.currentTimeSeconds()

version = project.modVersion
group = project.mavenGroup

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

loom {
    mixin.defaultRefmapName = 'nvidium.refmap.json'
    runs {
        it.configureEach {
            vmArgs('-Xmx8G', '-XX:+UseZGC')
        }
    }
}


processResources {
    inputs.properties('version': project.version, 'commit': gitCommitHash, 'buildtime': buildtime)

    archivesBaseName = 'nvidium'
    filesMatching('fabric.mod.json') {
        expand 'commit': gitCommitHash,
            'version': project.version,
            'buildtime': buildtime
    }
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = 'Modrinth'
                url = 'https://api.modrinth.com/maven'
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
    mappings "net.fabricmc:yarn:${project.yarnVersion}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loaderVersion}"

    // Fabric API
    modImplementation(fabricApi.module("fabric-api-base", project.fabricVersion))
    modImplementation(fabricApi.module("fabric-block-view-api-v2", project.fabricVersion))
    modImplementation(fabricApi.module("fabric-rendering-fluids-v1", project.fabricVersion))

    //modImplementation("net.fabricmc.fabric-api:fabric-rendering-data-attachment-v1:0.3.40+73761d2e3b")
    modImplementation(fabricApi.module('fabric-rendering-data-attachment-v1', project.fabricVersion))
    modImplementation(fabricApi.module('fabric-resource-loader-v0', project.fabricVersion))

    modImplementation "maven.modrinth:sodium:${project.sodiumVersion}"

    //modImplementation "maven.modrinth:c2me-fabric:0.2.0+alpha.10.49+1.19.4"
    //modImplementation "maven.modrinth:chunks-fade-in:v1.0.3-1.19.4"
    //modImplementation "maven.modrinth:immersiveportals:v2.7.3-mc1.19.4"
    modCompileOnly "maven.modrinth:iris:${project.irisVersion}"
}

